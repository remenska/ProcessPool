%%% START HERE
% sort TaskFunction = struct RegisterFile | RemoveFile | LogUpload;
sort Task = struct task(id:Nat,done:Bool) | None; 
sort Worker = struct W(id:Nat,isWorking:Bool, isAlive:Bool);
% sort ReturnMessage = struct S_OK | S_ERROR | S_FULL;
% FIXED Error while exploring state space: Expression !(0 < #tail([])) does not rewrite to true or false in the condition 
%if(e7_WorkingProcess_while2, if(e6_WorkingProcess_while2, s10_WorkingProcess_while2 == 14 && !(!stopEvent_WorkingProcess_while2 &&
% #__pendingQueue_Queues_mem > 0), s10_WorkingProcess_while2 == 14 && !stopEvent_WorkingProcess_while2 && #__pendingQueue_Queues_mem > 0), 
%if(e6_WorkingProcess_while2, s10...
% This is because "Note that the head and tail of an empty list are undefined, so that mCRL2 will not further reduce the terms head([]) and tail([])"


% BEGIN ProcessPool maps
% ================================
map mapGetNumWorkingProcesses: List(Worker) -> Nat;
var
    listWorkers:List(Worker);
    el:Worker;

eqn
    mapGetNumWorkingProcesses([]) = 0;
    isWorking(el) -> mapGetNumWorkingProcesses(el|>listWorkers) = 1 + mapGetNumWorkingProcesses(listWorkers);
    !(isWorking(el)) -> mapGetNumWorkingProcesses(el|>listWorkers) = mapGetNumWorkingProcesses(listWorkers);

% =================================
map mapGetNumIdleProcesses: List(Worker) -> Nat;
var
    listWorkers:List(Worker);
    el:Worker;

eqn
    mapGetNumIdleProcesses([]) = 0;
    isWorking(el) -> mapGetNumIdleProcesses(el|>listWorkers) = mapGetNumIdleProcesses(listWorkers);
    !(isWorking(el)) -> mapGetNumIdleProcesses(el|>listWorkers) = 1 + mapGetNumIdleProcesses(listWorkers);
% =================================

map mapSetWorking: Nat#Bool#List(Worker) -> List(Worker);
var
  listWorkers, updatedList:List(Worker);
  el:Worker;
  wpid:Nat;
  isWorking:Bool;

eqn
    mapSetWorking(wpid, isWorking, []) = [];
    (listWorkers!=[] && id(head(listWorkers))==wpid && isAlive(head(listWorkers))) ->  % FIXED: only alive processes set to working
	mapSetWorking(wpid, isWorking, listWorkers) = W(id(head(listWorkers)), isWorking,isAlive(head(listWorkers)))|> mapSetWorking(wpid, isWorking, tail(listWorkers));
    (listWorkers!=[] && id(head(listWorkers))!=wpid) -> 
	mapSetWorking(wpid, isWorking, listWorkers) = head(listWorkers)|> mapSetWorking(wpid, isWorking, tail(listWorkers));
    (listWorkers!=[] && id(head(listWorkers))==wpid && !isAlive(head(listWorkers))) ->   % FIXED: only alive processes set to working
	mapSetWorking(wpid, isWorking, listWorkers) = head(listWorkers)|> mapSetWorking(wpid, isWorking, tail(listWorkers));

map cleanDeadWorkers:List(Worker) -> List(Worker);
var
  listWorkers, updatedList:List(Worker);
  el:Worker;
  
eqn
  cleanDeadWorkers([]) = [];
  (listWorkers!=[]) && (!isAlive(head(listWorkers))) -> cleanDeadWorkers(listWorkers) = cleanDeadWorkers(tail(listWorkers));
  (listWorkers!=[]) && (isAlive(head(listWorkers))) -> cleanDeadWorkers(listWorkers) = head(listWorkers) |> cleanDeadWorkers(tail(listWorkers));
% =================================

map mapSetDead: Nat#List(Worker) -> List(Worker);
var
  listWorkers, updatedList:List(Worker);
  wpid:Nat;
  
eqn
  mapSetDead(wpid, []) = [];
  (listWorkers!=[] && id(head(listWorkers))==wpid) ->  
      mapSetDead(wpid,listWorkers) = W(id(head(listWorkers)),isWorking(head(listWorkers)),false)|> mapSetDead(wpid, tail(listWorkers));
  (listWorkers!=[] && id(head(listWorkers))!=wpid) ->  
       mapSetDead(wpid,listWorkers) = head(listWorkers)|> mapSetDead(wpid, tail(listWorkers));

% ===============================

% PARAMETERS:

map __requestsPerCycle, __minProcess, __maxProcess, __queueSize:Nat;
eqn __requestsPerCycle = 5;
    __minProcess = 1;
    __maxProcess = 2;
    __queueSize = 3;
% =================================
% END ProcessPool maps
% ================================


%% ============ ProcessPool
act r_getMaxSize, s_getMaxSize, getMaxSize:Nat;
act r_getMinSize, s_getMinSize, getMinSize:Nat;
act r_getNumWorkingProcesses, s_getNumWorkingProcesses, getNumWorkingProcesses:Nat;
act r_getNumIdleProcesses, s_getNumIdleProcesses, getNumIdleProcesses:Nat;


act s__prListLock_acquire,r__prListLock_acquire,__prListLock_acquire;
act s__prListLock_release,r__prListLock_release,__prListLock_release;



act s_getRequestsPerCycle, r_getRequestsPerCycle, getRequestsPerCycle:Nat;
act s_getMinProcess, r_getMinProcess, getMinProcess:Nat;
act s_getMaxProcess, r_getMaxProcess, getMaxProcess:Nat;
act s_getQueueSize, r_getQueueSize, getQueueSize:Nat;
act s_getRequest, r_getRequest, getRequest:Task;

act RequestExecutingAgent_return;

% BEGIN RequestExecutingAgent processes

proc RequestExecutingAgent_mem(__requestsPerCycle:Nat, minProcess:Nat, maxProcess:Nat, queueSize:Nat) = 
      r_getRequestsPerCycle(__requestsPerCycle).
     RequestExecutingAgent_mem(__requestsPerCycle, minProcess, maxProcess, queueSize) 
      + 
     r_getMaxProcess(maxProcess).
     RequestExecutingAgent_mem(__requestsPerCycle, minProcess, maxProcess, queueSize) 
      +
     r_getMinProcess(minProcess).
     RequestExecutingAgent_mem(__requestsPerCycle, minProcess, maxProcess, queueSize) 
      +
     r_getQueueSize(queueSize).
 RequestExecutingAgent_mem(__requestsPerCycle, minProcess, maxProcess, queueSize) 
;

% __requestsPerCycle:Nat, minProcess:Nat, maxProcess:Nat, queueSize:Nat
proc RequestExecutingAgent = RequestExecutingAgent_mem(__requestsPerCycle,__minProcess,__maxProcess,__queueSize) || processPoolSingleton || RequestExecutingAgent_execute(0);

% __pendingQueue:List(Task),__resultsQueue:List(Task), __maxPendingQueue:Nat
proc processPoolSingleton = Queues_mem([],[], __queueSize) ||
% sort ProcessPoolTuple = struct processPoolTuple(__minSize:Nat,__maxSize:Nat,__maxQueuedRequests:Nat, __workersDict:List(Worker), __draining:Bool, __stopEvent:Bool);
  ProcessPool_mem(processPoolTuple(__minProcess,__maxProcess,__queueSize,[],false)) 
	    || ProcessPool__spawnNeededWorkingProcesses || ProcessPool_createAndQueueTask || ProcessPool___backgroundProcess 
;

% RequestExecutingAgent method
proc RequestExecutingAgent_execute(taskCounter:Nat) =
    sum requestsPerCycle:Nat.s_getRequestsPerCycle(requestsPerCycle).
      (taskCounter<requestsPerCycle) -> (
	 sum newTask:Task.s_getRequest(newTask).
	  c_s_getNumIdleProcesses.sum numIdle:Nat.r_r_getNumIdleProcesses(numIdle).
	  c_s_getNumWorkingProcesses.sum numWorking:Nat.r_r_getNumWorkingProcesses(numWorking).
	  RequestExecutingAgent_execute_while(taskCounter, newTask)
	 
	).RequestExecutingAgent_execute(taskCounter+1) <> RequestExecutingAgent_return; 

% RequestExecutingAgent method

act s_getFreeSlots, r_getFreeSlots, getFreeSlots:Nat;
act c_s_createAndQueueTask, c_r_createAndQueueTask, c__createAndQueueTask:Task;
% act r_s_createAndQueueTask, r_r_createAndQueueTask, r__createAndQueueTask:ReturnMessage;

act time_sleep;   
proc RequestExecutingAgent_execute_while(taskCounter:Nat, newTask:Task) = 
    sum freeSlots:Nat.s_getFreeSlots(freeSlots).
    (freeSlots==0) -> (
    time_sleep.RequestExecutingAgent_execute_while(taskCounter, newTask) 
    ) <> c_s_createAndQueueTask(newTask)
    %sum retMsg:ReturnMessage.r_r_createAndQueueTask(retMsg).
%	(retMsg!=S_OK) -> 
% 	.RequestExecutingAgent_execute_while(taskCounter,newTask) % <-- THIS LINE WAS OFFENDING!!!
%	<> return % this is in case if not enqueue["OK"]: self.log.error( enqueue["Message"] )
;

% ProcessPool method
proc ProcessPool_createAndQueueTask = 
    sum newTask:Task.c_r_createAndQueueTask(newTask).
    s__prListLock_acquire.  
    s___pendingQueuePut(newTask).
 % EDITED/COMMENTED: it will just block until there is space in the queue again, because blocking = True in the implementation
%     sum retMsg:ReturnMessage.r___pendingQueuePut_return(retMsg).
    s__prListLock_release.
%     ((retMsg==S_OK) ->
    ProcessPool__spawnNeededWorkingProcesses
%     ).
 %   r_s_createAndQueueTask(S_OK)
.ProcessPool_createAndQueueTask;


proc ProcessPool__spawnNeededWorkingProcesses = 
    ProcessPool__cleanDeadProcesses.
    sum __draining:Bool.s_getDraining(__draining).
  (__draining) -> (return) <> 
	(
	  ProcessPool__spawnNeededWorkingProcesses_while.
	  ProcessPool__spawnNeededWorkingProcesses_while1
	  )
;

proc ProcessPool__spawnNeededWorkingProcesses_while = 
    sum workersDict:List(Worker).s_getWorkersDict(workersDict).sum __minSize:Nat.s_getMinSize(__minSize).
    (#workersDict < __minSize) -> 
    (
    ProcessPool__spawnWorkingProcess
    .ProcessPool__spawnNeededWorkingProcesses_while
    ) <> return
;

proc ProcessPool__spawnNeededWorkingProcesses_while1 = 
	  c_s_getNumIdleProcesses.sum numIdle:Nat.r_r_getNumIdleProcesses(numIdle).
	  sum pqSize:Nat.s___pendingQueueSize(pqSize).
	  sum workersDict:List(Worker).s_getWorkersDict(workersDict).
	  sum __maxSize:Nat.s_getMaxSize(__maxSize).
	(#workersDict < __maxSize && pqSize>0 && numIdle==0) ->
	(
	    ProcessPool__spawnWorkingProcess
	    .ProcessPool__spawnNeededWorkingProcesses_while1
    ) <> return
;


act s_activateWorkingProcess, r_activateWorkingProcess, activateWorkingProcess:Nat;

proc ProcessPool__spawnWorkingProcess = 
    s__prListLock_acquire.
    sum newwpid:Nat.s__createWorkingProcess(newwpid).
    s_addWorker(W(newwpid,false,true)).
    s_activateWorkingProcess(newwpid).
    s__prListLock_release
;

act s___cleanDeadProcesses, r___cleanDeadProcesses, __cleanDeadProcesses;


proc ProcessPool__cleanDeadProcesses = 
  s__prListLock_acquire.
  s___cleanDeadProcesses.
  s__prListLock_release
  ;
  
act s__createWorkingProcess, r__createWorkingProcess, createWorkingProcess:Nat; % only on reply a parameter is sent (wpid for new WorkingProcess) 

% keeps track of available WorkingProcess id's (those who are dead), assigns the dead ones back to newly created processes
proc OS_ops( workerIDs:List(Nat)) = 
   (workerIDs!=[])-> r__createWorkingProcess(head(workerIDs)).OS_ops(tail(workerIDs)) 
   +
   sum deadWorkerID:Nat.r_setDead2(deadWorkerID).OS_ops(deadWorkerID|>workerIDs)
;

%% creates tasks....
% proc RequestClient = 
%     (
%     r_getRequest(task(1,false, LogUpload)) + r_getRequest(task(1,false, RegisterFile)) + r_getRequest(task(2,false, RemoveFile))
%     )
% .RequestClient
% ;

proc RequestClient = 
    (
    r_getRequest(task(1,false)).
    r_getRequest(task(2,false)).
    r_getRequest(task(3,false)).
    r_getRequest(task(4,false)).
    r_getRequest(task(5,false)).
    r_getRequest(task(6,false))
    )
% .RequestClient
;

% END RequestExecutingAgent processes


act c_s_getNumWorkingProcesses, c_r_getNumWorkingProcesses, c__getNumWorkingProcesses;
act r_s_getNumWorkingProcesses, r_r_getNumWorkingProcesses, r__getNumWorkingProcesses:Nat;
act c_s_getNumIdleProcesses, c_r_getNumIdleProcesses, c__getNumIdleProcesses;
act r_s_getNumIdleProcesses, r_r_getNumIdleProcesses, r__getNumIdleProcesses:Nat; 

act r_getWorkersDict, s_getWorkersDict, getWorkersDict:List(Worker);
sort ProcessPoolTuple = struct processPoolTuple(__minSize:Nat,__maxSize:Nat,__maxQueuedRequests:Nat, __workersDict:List(Worker), __draining:Bool);

% BEGIN ProcessPool processes ========

act r_getDraining, s_getDraining, getDraining:Bool;
act r_addWorker, s_addWorker, addWorker:Worker;

act numWorkers:Nat;

% ProcessPool memory
proc ProcessPool_mem(ppt:ProcessPoolTuple) = 
        r_getMaxSize(__maxSize(ppt)).
	  ProcessPool_mem(ppt)
        +
        r_getMinSize(__minSize(ppt)).
        ProcessPool_mem(ppt)
	+
	r_getNumWorkingProcesses(mapGetNumWorkingProcesses(__workersDict(ppt))).
	ProcessPool_mem(ppt)
	+
	r_getNumIdleProcesses(mapGetNumIdleProcesses(__workersDict(ppt))).
	ProcessPool_mem(ppt)
	+
	sum wpid:Nat,working:Bool.r_set__working1(wpid, working). 
       ProcessPool_mem(processPoolTuple(__minSize(ppt), __maxSize(ppt), __maxQueuedRequests(ppt), mapSetWorking(wpid, working, __workersDict(ppt)), __draining(ppt)))
	+
	r_getFreeSlots( max(0, __maxSize(ppt) -  mapGetNumWorkingProcesses(__workersDict(ppt) ) )). % DONE: debug here why Int2Nat(-1)
      ProcessPool_mem(ppt)
	+ 
	r___cleanDeadProcesses.
	ProcessPool_mem(processPoolTuple(__minSize(ppt), __maxSize(ppt), __maxQueuedRequests(ppt), cleanDeadWorkers(__workersDict(ppt)), __draining(ppt)))
	+
	r_getDraining(__draining(ppt)).
	ProcessPool_mem(ppt)
% 	+
% 	r_getStopEvent(__stopEvent(ppt)). 
% 	ProcessPool_mem(ppt)
	+
	r_getWorkersDict(__workersDict(ppt)).
	ProcessPool_mem(ppt)
	+
% 	numWorkers(#__workersDict(ppt)).ProcessPool_mem(ppt) % temp for debugging, TODO: remove it
% 	+
	sum newWorker:Worker.r_addWorker(newWorker).
        ProcessPool_mem(processPoolTuple(__minSize(ppt), __maxSize(ppt), __maxQueuedRequests(ppt), __workersDict(ppt)<|newWorker, __draining(ppt)))
% 	+
% 	sum newStopEvent:Bool.r_setStopEvent(newStopEvent).
% 	ProcessPool_mem(processPoolTuple(__minSize(ppt), __maxSize(ppt), __maxQueuedRequests(ppt), __workersDict(ppt), __draining(ppt), newStopEvent))
	+
	sum wpid:Nat.r_setDead1(wpid).
	ProcessPool_mem(processPoolTuple(__minSize(ppt), __maxSize(ppt), __maxQueuedRequests(ppt), mapSetDead(wpid, __workersDict(ppt)), __draining(ppt)))
;


proc __prListLock =  
	r__prListLock_acquire.
	r__prListLock_release.
__prListLock
;


% ProcessPool method
proc ProcessPool_getNumWorkingProcesses = 
  c_r_getNumWorkingProcesses.
  s__prListLock_acquire. 
    sum wp:Nat.s_getNumWorkingProcesses(wp).
  s__prListLock_release.
  r_s_getNumWorkingProcesses(wp).
ProcessPool_getNumWorkingProcesses
;

% ProcessPool method
proc ProcessPool_getNumIdleProcesses = 
  c_r_getNumIdleProcesses.
  s__prListLock_acquire. 
    sum wp:Nat.s_getNumIdleProcesses(wp).
  s__prListLock_release.
r_s_getNumIdleProcesses(wp).
ProcessPool_getNumIdleProcesses
;

act internal, taskCallback;

proc ProcessPool___backgroundProcess =  % for daemonize
  sum __draining:Bool.s_getDraining(__draining).
  (!__draining) -> 
    (
      ProcessPool_processResults
    ).ProcessPool___backgroundProcess <> return;

proc ProcessPool_processResults = 
  ProcessPool__cleanDeadProcesses.
  sum pqSize:Nat.s___pendingQueueSize(pqSize).
  ((pqSize!=0) -> (
		    ProcessPool__spawnNeededWorkingProcesses
		    )
  <> internal). 
  sum rqSize:Nat.s___resultsQueueSize(rqSize).
  ((rqSize!=0) -> (
  sum resultsTask:Task.s___resultsQueueGet(resultsTask).  
		  taskCallback.ProcessPool_processResults
		
   ) <> return )
;


% END ProcessPool processes ========


%% ============ START WorkingProcess
act r_isWorking, s_isWorking, isWorking:Nat#Bool;
act r_taskProcessed, s_taskProcessed, taskProcessed:Nat#Nat;
act r_runWatchdogThread, s_runWatchdogThread, runWatchdogThread:Nat#Nat;
act doSomethingWatchdog:Nat#Nat;
act doSomethingWorkingProcess;
act r_set__working, r_set__working1, s_set__working, set__working:Nat#Bool;
act r_runProcessThread, s_runProcessThread, runProcessThread:Nat#Nat;
act r_setTask, s_setTask, setTask:Nat#Task;
% act processTask, createProcessThread, 
act return;
act r_increaseTaskCounter, s_increaseTaskCounter, increaseTaskCounter:Nat;
act r_getStopEvent, s_getStopEvent, getStopEvent:Bool; 
act r_clearTask, s_clearTask, clearTask:Nat;
% act r_joinProcessThread, s_joinProcessThread, joinProcessThread;
act r___pendingQueueGet, s___pendingQueueGet, __pendingQueueGet:Task#Nat;
act r___pendingQueuePut, s___pendingQueuePut, __pendingQueuePut:Task;
% act r___pendingQueuePut_return, s___pendingQueuePut_return, __pendingQueuePut_return:ReturnMessage;

act r___resultsQueuePut, s___resultsQueuePut, __resultsQueuePut:Task;
act r___resultsQueueGet, s___resultsQueueGet, __resultsQueueGet:Task;

act r___pendingQueueSize, s___pendingQueueSize, __pendingQueueSize:Nat;
act r___resultsQueueSize, s___resultsQueueSize, ___resultsQueueSize:Nat;
act r_setStopEvent, s_setStopEvent, setStopEvent:Bool;

%%% ============= START Memory for queueues, these are shared between the ProcessPool and the Workers, this is how they communicate

proc Queues_mem(__pendingQueue:List(Task),__resultsQueue:List(Task), __maxPendingQueue:Nat) =   
   (__pendingQueue!=[])-> sum wpid:Nat.r___pendingQueueGet(head(__pendingQueue), wpid). 
    Queues_mem(tail(__pendingQueue), __resultsQueue, __maxPendingQueue)
  +
  (__pendingQueue==[])-> sum wpid:Nat.r___pendingQueueGet(None,wpid).s_setDead(wpid).
    Queues_mem(__pendingQueue, __resultsQueue, __maxPendingQueue)
   + 
   sum newTask:Task.r___resultsQueuePut(newTask).
    Queues_mem(__pendingQueue, __resultsQueue<|newTask, __maxPendingQueue) 
      +
    r___pendingQueueSize(#__pendingQueue).
    Queues_mem(__pendingQueue, __resultsQueue, __maxPendingQueue) 
      +
    r___resultsQueueSize(#__resultsQueue).
    Queues_mem(__pendingQueue, __resultsQueue, __maxPendingQueue) 
      +
%  pendingQueue will block putting elements beyond the max, doesn't raise exception!!!! Because blocking = True in the implementation by default
%  (s___pendingQueuePut_return(S_FULL).Queues_mem(__pendingQueue, __resultsQueue, __maxPendingQueue))  <> 
%    (s___pendingQueuePut_return(S_OK).Queues_mem(__pendingQueue<|newTask, __resultsQueue, __maxPendingQueue))
    (__maxPendingQueue> #__pendingQueue) -> 
    sum newTask:Task.r___pendingQueuePut(newTask).Queues_mem(__pendingQueue<|newTask, __resultsQueue, __maxPendingQueue)
    
    +
    
% does it block if there is nothing in the queue, or should I just return None?
% CHECKED, IT'S BLOCKING, so just commented these below!!!!
 
%  (__resultsQueue==[])-> r___resultsQueueGet(None).  
%    Queues_mem(__pendingQueue, __resultsQueue, __maxPendingQueue)
%     +
   (__resultsQueue!=[])-> r___resultsQueueGet(head(__resultsQueue)). 
    Queues_mem(__pendingQueue, tail(__resultsQueue), __maxPendingQueue)
    
;

%%% ============= END Memory for queueues

% BEGIN WorkingProcess processes
% 
%%% WorkingProcess Memory

sort WorkingProcessTuple = struct workingProcessTuple(wpid:Nat, __working:Bool,__taskCounter:Nat,__watchdogThread:Nat, __processThread:Nat,task:Task);

act r_setDead, r_setDead1, r_setDead2, s_setDead, setDead:Nat;

proc WorkingProcess_mem(wpt:WorkingProcessTuple) = 
      r_isWorking(wpid(wpt),__working(wpt)). 
      WorkingProcess_mem(wpt)
    +
      r_taskProcessed(wpid(wpt),__taskCounter(wpt)). % tell how many tasks have been processed so far
      WorkingProcess_mem(wpt)
    +
       sum working:Bool.r_set__working(wpid(wpt), working). 
       WorkingProcess_mem(workingProcessTuple(wpid(wpt), working, __taskCounter(wpt), __watchdogThread(wpt), __processThread(wpt), task(wpt)))
    +
      sum newTask:Task.r_setTask(wpid(wpt), newTask).
     WorkingProcess_mem(workingProcessTuple(wpid(wpt), __working(wpt), __taskCounter(wpt), __watchdogThread(wpt), __processThread(wpt), newTask))
    +
      r_increaseTaskCounter(wpid(wpt)). 
     WorkingProcess_mem(workingProcessTuple(wpid(wpt), __working(wpt), (__taskCounter(wpt)+1), __watchdogThread(wpt), __processThread(wpt), task(wpt)))
    + 
     r_clearTask(wpid(wpt)).
    WorkingProcess_mem(workingProcessTuple(wpid(wpt), __working(wpt), __taskCounter(wpt), __watchdogThread(wpt), __processThread(wpt), None))
    +
    r_setDead(wpid(wpt)).WorkingProcess_mem(workingProcessTuple(wpid(wpt),false,0,1,1,None))
;

proc WatchdogThread(wpid:Nat, wdtid:Nat) = doSomethingWatchdog(wpid,wdtid).WatchdogThread(wpid,wdtid);  


act s_forkProcessThread, r_forkProcessThread, forkProcessThread:Nat#Nat;
act s_joinProcessThread, r_joinProcessThread, joinProcessThread:Nat#Nat;

act processTask:Nat;

proc ProcessThread(wpid:Nat, ptid:Nat)= 
    r_forkProcessThread(wpid,ptid).
	runProcessThread(wpid,ptid).processTask(ptid).
    r_joinProcessThread(wpid,ptid)
.ProcessThread(wpid, ptid)
; 


proc WorkingProcess(wpid:Nat) = 
WorkingProcess_start(wpid) || WorkingProcess_mem(workingProcessTuple(wpid,false,0,1,1,None));

%WorkingProcess_start(wpid:Nat) = s_runWatchdogThread(wpid, 1);

% TODO: revert to this, for now let's ignore watchdog thread
% proc WorkingProcess_start(wpid:Nat) = r_activateWorkingProcess(wpid).runWorkingProcess.WatchdogThread(wpid, 1) || 
act runWorkingProcess:Nat;
proc WorkingProcess_start(wpid:Nat) = r_activateWorkingProcess(wpid).
					  runWorkingProcess(wpid).
					    WorkingProcess_while(wpid, 0).
			  WorkingProcess_start(wpid)
				;

proc WorkingProcess_while(wpid:Nat, idleLoopCount:Nat) = 
% 		sum draining:Bool.s_getDraining(draining). 
% 		(idleLoopCount >= 3 || draining) -> s_setDead(wpid).return <> ( % I have set idleLoopCount to smaller value = 3 instead of 10 )
			sum taskFromQueue:Task.s___pendingQueueGet(taskFromQueue, wpid). 
			(taskFromQueue!=None) -> (
				 s_clearTask(wpid).
				 s_set__working(wpid, true).
				 s_setTask(wpid, taskFromQueue). 
				 s_forkProcessThread(wpid,wpid+10).
				 s_joinProcessThread(wpid,wpid+10).
      %             if not self.task.taskResults() and not self.task.taskException():
				 s___resultsQueuePut(task(id(taskFromQueue), true)).
				 s_increaseTaskCounter(wpid).
				 s_set__working(wpid, false)   
				). WorkingProcess_while(wpid, 0)
				<> return
				;
				    
% END WorkingProcess processes


init allow(  { getMaxSize, getMinSize,
	  getNumWorkingProcesses,
	  getNumIdleProcesses,
	  __prListLock_acquire,
	  __prListLock_release,
	  isWorking,
	  taskProcessed,
	  runWatchdogThread,
	  set__working,
	  setTask,
	  increaseTaskCounter,
	  runProcessThread,
	  getStopEvent,
	  clearTask,
	  joinProcessThread,
	  __pendingQueueGet,
	  __resultsQueuePut,
	  __pendingQueueSize,
	  setStopEvent,
	  getRequestsPerCycle,
	  getMinProcess,
	  getMaxProcess,
	  getQueueSize,
	  getRequest,
	  return,
	  c__getNumWorkingProcesses,
	  r__getNumWorkingProcesses,
	  c__getNumIdleProcesses,
	  r__getNumIdleProcesses,
	  runWorkingProcess,
	  processTask,
	  doSomethingWatchdog,
	  RequestExecutingAgent_return,
	  getFreeSlots,
	  c__createAndQueueTask, 
%	  r__createAndQueueTask,
	  time_sleep,
	  __pendingQueuePut,
	  __cleanDeadProcesses,
	  getDraining,
	  getWorkersDict,
	  createWorkingProcess,
% 	  isAlive,
	  addWorker,
	  internal,
	  __resultsQueueGet,
	  taskCallback,
	  setDead,
%	  __pendingQueuePut_return,
	  activateWorkingProcess,
	  ___resultsQueueSize,
	forkProcessThread,
	numWorkers

	  },
       comm( { r_getMaxSize|s_getMaxSize->getMaxSize,
        r_getMinSize | s_getMinSize -> getMinSize,
        r_getNumWorkingProcesses | s_getNumWorkingProcesses -> getNumWorkingProcesses,
        r_getNumIdleProcesses | s_getNumIdleProcesses -> getNumIdleProcesses,
        s__prListLock_acquire | r__prListLock_acquire -> __prListLock_acquire,
        s__prListLock_release | r__prListLock_release -> __prListLock_release,
        s_isWorking | r_isWorking -> isWorking,
        s_taskProcessed | r_taskProcessed -> taskProcessed,
        s_runWatchdogThread | r_runWatchdogThread -> runWatchdogThread,
	s_set__working | r_set__working | r_set__working1 -> set__working,
	r_setTask | s_setTask -> setTask,
	r_increaseTaskCounter | s_increaseTaskCounter -> increaseTaskCounter,
	r_runProcessThread | s_runProcessThread -> runProcessThread,
	r_getStopEvent | s_getStopEvent -> getStopEvent,
	r_clearTask | s_clearTask -> clearTask,
	r_joinProcessThread | s_joinProcessThread -> joinProcessThread,
	r___pendingQueueGet | s___pendingQueueGet -> __pendingQueueGet,
	r___resultsQueuePut |  s___resultsQueuePut -> __resultsQueuePut,
	r___pendingQueueSize | s___pendingQueueSize -> __pendingQueueSize,
	r_setStopEvent | s_setStopEvent -> setStopEvent,
	s_getRequestsPerCycle | r_getRequestsPerCycle -> getRequestsPerCycle,
	s_getMinProcess | r_getMinProcess -> getMinProcess,
	s_getMaxProcess | r_getMaxProcess -> getMaxProcess,
	s_getQueueSize | r_getQueueSize -> getQueueSize,
	s_getRequest | r_getRequest -> getRequest,
	c_s_getNumWorkingProcesses | c_r_getNumWorkingProcesses -> c__getNumWorkingProcesses,
	r_s_getNumWorkingProcesses | r_r_getNumWorkingProcesses -> r__getNumWorkingProcesses,
	r_s_getNumIdleProcesses | r_r_getNumIdleProcesses -> r__getNumIdleProcesses,
	c_s_getNumIdleProcesses | c_r_getNumIdleProcesses -> c__getNumIdleProcesses,
	s_getFreeSlots | r_getFreeSlots -> getFreeSlots,
	c_s_createAndQueueTask | c_r_createAndQueueTask -> c__createAndQueueTask,
%	r_s_createAndQueueTask | r_r_createAndQueueTask -> r__createAndQueueTask,
	r___pendingQueuePut | s___pendingQueuePut ->  __pendingQueuePut,
	r___cleanDeadProcesses | s___cleanDeadProcesses -> __cleanDeadProcesses,
	r_getDraining | s_getDraining -> getDraining,
	r_getWorkersDict | s_getWorkersDict -> getWorkersDict,
	r__createWorkingProcess | s__createWorkingProcess -> createWorkingProcess,
	r_addWorker | s_addWorker -> addWorker,
	r___resultsQueueGet | s___resultsQueueGet -> __resultsQueueGet,
	r_setDead | r_setDead1 | r_setDead2 | s_setDead -> setDead,
%	r___pendingQueuePut_return | s___pendingQueuePut_return -> __pendingQueuePut_return,
	s_activateWorkingProcess | r_activateWorkingProcess -> activateWorkingProcess,
	r___resultsQueueSize | s___resultsQueueSize ->  ___resultsQueueSize,
	s_forkProcessThread | r_forkProcessThread -> forkProcessThread
       },
       %% === Agent stuff
       RequestExecutingAgent || RequestClient ||
       
       %% ==== ProcessPool stuff
	 __prListLock || 
	 ProcessPool_getNumWorkingProcesses || ProcessPool_getNumIdleProcesses

         || OS_ops([1,2])
	 || WorkingProcess(1) 
	 || WorkingProcess(2) 
	 || ProcessThread(1,11)
	 || ProcessThread(2,12)
% 	 || WorkingProcess(3) 
     ));
     
     
